/* Definition section */
%{
#include "y.tab.h"	/* header file generated by bison */

#include <string.h>

#define BUF_SIZE 256
extern YYSTYPE yylval; //YYSTYPE
char buf[BUF_SIZE];

int dump_flag = 0;
extern void dump_symbol();

/* This macro is used to record code line */
#define CONCAT strncat(buf, yytext, BUF_SIZE - strlen(buf) - 1)
%}

/* Define regular expression label */
digit		[0-9]
letter		[a-zA-Z]
integer     {digit}+
float       {digit}+"."{digit}+
id			{letter}+({letter}|{digit})*

%option yylineno

%x COMMENT STRING_STATE

/* Rules section */
%%

 /* Arithmetic */
"+"     { CONCAT; return(ADD); }
"-" 	{ CONCAT; return(SUB); }
"*" 	{ CONCAT; return(MUL); }
"/" 	{ CONCAT; return(DIV); }
"%"     { CONCAT; return(MOD); }
"++"    { CONCAT; return(INC); }
"--"    { CONCAT; return(DEC); }

 /* Relational */
">"		{ CONCAT; return(MT); }
"<"		{ CONCAT; return(LT); }
">="	{ CONCAT; return(MTE); }
"<="	{ CONCAT; return(LTE); }
"=="	{ CONCAT; return(EQ); }
"!="	{ CONCAT; return(NE); }

 /* Assignment */
"="		{ CONCAT; return(ASGN); }
"+="	{ CONCAT; return(ADDASGN); }
"-="	{ CONCAT; return(SUBASGN); }
"*="	{ CONCAT; return(MULASGN); }
"/="	{ CONCAT; return(DIVASGN); }
"%="	{ CONCAT; return(MODASGN); }

 /* Logical */
"&&"	{ CONCAT; return(AND); }
"||"	{ CONCAT; return(OR); }
"!"		{ CONCAT; return(NOT); }

 /* Delimiters */
"("		{ CONCAT; return(LB); }
")"		{ CONCAT; return(RB); }
"{"		{ CONCAT; return(LCB); }
"}"		{ CONCAT; return(RCB); }
"["		{ CONCAT; return(LSB); }
"]"		{ CONCAT; return(RSB); }
","		{ CONCAT; return(COMMA); }

 /* Print Keywords */	
"print"		{ CONCAT; return(PRINT); }

 /* Condition and Loop Keywords */
"if"		{ CONCAT; return(IF); }
"else"		{ CONCAT; return(ELSE); }
"while"		{ CONCAT; return(WHILE); }

 /* Declaration Keywords */
"void"		{ CONCAT; return(VOID); }
"int"		{ CONCAT; return(INT); }
"float"  	{ CONCAT; return(FLOAT); }
"string"  	{ CONCAT; return(STRING); }
"bool"  	{ CONCAT; return(BOOL); }

 /* boolean Keywords */
"true"      { CONCAT; yylval.i_val = 1; return(TRUE); }
"false"     { CONCAT; yylval.i_val = 0; return(FALSE); }
"return"    { CONCAT; return(RETURN); }

 /* String Constant */
\"				        { BEGIN STRING_STATE; CONCAT; return(QUOTA);}
<STRING_STATE>\"		{ BEGIN INITIAL; CONCAT; return(QUOTA);}
<STRING_STATE>[^"]*		{ CONCAT; return(STR_CONST); }

 /* Number Constant */
{integer}	{ CONCAT; yylval.i_val = atoi(yytext); return(I_CONST); }
{float}	    { CONCAT; yylval.f_val = atof(yytext); return(F_CONST); }

 /* C type Comment */
"/*"				{ BEGIN COMMENT; CONCAT; }
<COMMENT>[^*\n]+	{ CONCAT; }
<COMMENT>\n			{ CONCAT; printf("%d: %s", yylineno, buf); strcpy(buf,"");}
<COMMENT>"*"		{ CONCAT;  }
<COMMENT>"*/"		{  BEGIN INITIAL;CONCAT;}

 /* C++ type Comment */
\/\/.*	{ CONCAT; }

 /* Variable ID */
 {id}       { CONCAT; yylval.string = strdup(yytext); return(ID); }

 /* others */

[;]         { CONCAT; return(SEMICOLON); }
[\n]        { CONCAT; printf("%d: %s", yylineno, buf); strcpy(buf,"");
                if(dump_flag) {
                    dump_symbol();
                    dump_flag = 0;
                }
}
[ \t]		{ CONCAT; } /* Ignore */
[^ \t\n] 	{ CONCAT; } /* Ignore other charactor sets */

%%

int yywrap()
{
    return 1;
}
